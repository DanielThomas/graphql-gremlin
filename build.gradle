buildscript {
    repositories {
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
        jcenter()
    }

    dependencies {
        classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:latest.release'
        classpath 'com.netflix.nebula:nebula-release-plugin:4.+'
    }
}

apply plugin: 'java'
apply plugin: 'groovy' // for tests
apply plugin: 'me.champeau.gradle.antlr4'
apply plugin: 'nebula.nebula-release'

group = 'io.jschneider'

repositories {
    jcenter()
}

antlr4.output = new File('src/main/java/io/jschneider/graphql/gremlin/grammar')
antlr4.extraArgs = ['-package', 'io.jschneider.graphql.gremlin.grammar']

dependencies {
    compile 'org.apache.tinkerpop:gremlin-core:3.1.1-incubating'
    compile 'org.projectlombok:lombok:1.16.6'
    compile 'com.google.guava:guava:18.0'

    testCompile 'org.apache.tinkerpop:tinkergraph-gremlin:3.1.1-incubating'
    testCompile 'org.apache.tinkerpop:gremlin-groovy:3.1.1-incubating'
    testCompile 'junit:junit:latest.release'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

// add antlr4 to classpath
configurations {
    compile.extendsFrom antlr4
}

tasks.release.finalizedBy tasks.build

bintray {
    user = 'jkschneider'
    key = bintrayApiKey
    dryRun = false
    publish = true

    pkg {
        repo = 'jons-oss'
        name = 'graphql-gremlin'
        vcsUrl = 'git@github.com:jkschneider/graphql-gremlin.git'
        websiteUrl = 'https://github.com/jkschneider/graphql-gremlin'
        issueTrackerUrl = 'https://github.com/jkschneider/graphql-gremlin/issues'
        licenses = ['Apache-2.0']
        labels = ['graph', 'tinkerpop', 'graphql', 'data']
        publicDownloadNumbers = true
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}